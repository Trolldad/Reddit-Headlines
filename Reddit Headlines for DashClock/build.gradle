buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.8.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.1'
    }
}
apply plugin: 'android'
apply plugin: 'android-apt'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    if(new File("../keystore.properties").exists()) {

        Properties props = new Properties()
        props.load(new FileInputStream(file("../keystore.properties")))

        signingConfigs {
            release {
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile 'com.android.support:support-v13:19.0.1'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    apt 'org.androidannotations:androidannotations:3.0'
    compile 'org.androidannotations:androidannotations-api:3.0'
    compile 'com.squareup.okhttp:okhttp:1.2.1'
    compile 'joda-time:joda-time:2.3'
    compile 'it.sephiroth.android.library.imagezoom:library:+'
    compile 'com.squareup.retrofit:retrofit:1.3.0'
    compile 'com.squareup.picasso:picasso:2.1.1'
    compile 'org.ocpsoft.prettytime:prettytime:3.2.4.Final'
    compile 'com.squareup:otto:1.3.4'
    compile project(':ViewPagerIndicator')
}

afterEvaluate {
    android.applicationVariants.each { variant ->
        def aptOutputDir = file("${project.buildDir}/source/apt/${variant.dirName}")

        variant.javaCompile.doFirst {
            aptOutputDir.mkdirs()
            variant.javaCompile.options.compilerArgs += [
                    '-processorpath', configurations.apt.getAsPath(),
                    '-AandroidManifestFile=' + variant.processResources.manifestFile,
                    '-s', aptOutputDir
            ]
        }
    }
}
